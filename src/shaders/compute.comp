#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

#define N 20//(1 << 14)     // a fraction of every N blades got culled

#define RECOVERY_FORCE 1
#define GRAVITY 1
#define WIND 1

#define ORIENTATION_CULL 1
#define DISTANCE_CULL 1
#define FRUSTUM_CULL 1

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
    vec3 camPos;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;    // xyz, dir
    vec4 v1;    // xyz, height
    vec4 v2;    // xyz, width
    vec4 up;    // xyz, stiffness
};

// TODO: Add bindings to:
// 1. Store the input blades
layout(set = 2, binding = 0) buffer InputBlades {
    Blade blades[];
} inputBlades;

// 2. Write out the culled blades
layout(set = 2, binding = 1) buffer CulledBlades {
    Blade blades[];
} outputBlades;

// 3. Write the total number of blades remaining
layout(set = 2, binding = 2) buffer NumBlades {
    uint vertexCount;   // Write the number of blades remaining here
    uint instanceCount; // = 1
    uint firstVertex;   // = 0
    uint firstInstance; // = 0
} numBlades;

// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like
//
// layout(set = ???, binding = ???) buffer NumBlades {
// 	  uint vertexCount;   // Write the number of blades remaining here
// 	  uint instanceCount; // = 1
// 	  uint firstVertex;   // = 0
// 	  uint firstInstance; // = 0
// } numBlades;

vec3 wind1(vec2 v0xz, float time)
{
    vec3 dir = normalize(vec3(1.0, 0.0, 0.3)); // main wind direction

    float wavelength = 50.0;
    float speed      = 1.2;
    float strength   = 5.0;

    // phi(v0, t) = 2pi/wavelength * dot(pos, dir) - speed * t
    float phase = dot(v0xz, dir.xz) * (2.0 * 3.14 / wavelength)
                  - time * speed;

    float base = 1/2.f * (sin(phase) + 1);

    // small secondary noise-like wave
    float gust = 0;// 0.35 * sin(phase * 4.0 + time * 1.9);

    float w = strength * (base + gust);
    return dir * w;
}

vec3 wind2(vec2 v0xz, float time)
{
    vec3 dir = normalize(vec3(-1.0, 0.0, -0.3)); // main wind direction

    float wavelength = 50.0;
    float speed      = 1.2;
    float strength   = 5.0;

    // phi(v0, t) = 2pi/wavelength * dot(pos, dir) - speed * t
    float phase = 3.14 + dot(v0xz, dir.xz) * (2.0 * 3.14 / wavelength)
                  - time * speed;

    float base = 1/2.f * (sin(phase) + 1);

    // small secondary noise-like wave
    float gust =  0; // 0.35 * sin(phase * 4.0 + time * 1.9);

    float w = strength * (base + gust);
    return dir * w;
}

void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		 numBlades.vertexCount = 0;
	}
	barrier(); // Wait till all threads reach this point

    uint bladeIdx = gl_GlobalInvocationID.x;
    Blade curBlade = inputBlades.blades[bladeIdx];
    float height = curBlade.v1.w;
    float stiffness = curBlade.up.w;

    // TODO: Apply forces on every blade and update the vertices in the buffer
    vec3 naturalForce = vec3(0.0, 0.0, 0.0); 
    vec3 v0 = curBlade.v0.xyz;
    vec3 v1 = curBlade.v1.xyz;
    vec3 v2 = curBlade.v2.xyz;
    vec3 up = curBlade.up.xyz;

#if RECOVERY_FORCE
    vec3 initV2Pos = v0 + up * height;
    vec3 r = (initV2Pos - v2) * stiffness;
    naturalForce += r;
#endif

    vec3 G_dir = vec3(0.0, -1.0, 0.0);
    float G_magnitude = 9.81;
    vec3 ge = 0.5f * G_dir * G_magnitude;                   // mass * G_dir * G_magnitude, environment gravity
    float angle = curBlade.v0.w;
    vec3 t1 = normalize( vec3(cos(angle), 0, sin(angle)) ); // bitangent t1
    vec3 faceDir = normalize(cross(up, t1));                           // normal vector of the blade face
#if GRAVITY
    vec3 gf = 1/4.f * length(ge) * faceDir;                 // front gravity
    vec3 g = ge + gf;
    naturalForce += g;
#endif

#if WIND
    vec3 wi = wind1(curBlade.v0.xz, totalTime) + wind2(curBlade.v0.xz, totalTime);
    float f_dir = 1 - abs( dot(normalize(wi), normalize(v2 - v0)) );  // dirtectional alignment
    float f_r = dot(v2 - v0, up) / height;                            // height ratio
    float theta = f_dir * f_r;                                        // alignment value

    vec3 w = wi * theta;
    naturalForce += w;
#endif
    
    vec3 corrV2 = v2 + naturalForce * deltaTime;
    vec3 corrV1 = v1;

    // 5.2 State validation
    corrV2 = corrV2 - up * min(dot(up, (corrV2 - v0)), 0);
    
    // v1 pos validation
    vec3 proj_up = dot(up, corrV2 - v0) * up;
    float l_proj = length(corrV2 - v0 - up * proj_up );      // length of vec v0 to v2 projected onto the ground
    corrV1 = v0 + height * up * max(1.0f - l_proj/height, 0.05 * max(l_proj/height, 1.0f));

    // length validation
    float L0 = distance(v0, corrV2);
    float L1 = distance(v0, corrV1) + distance(corrV1, corrV2);
    float L = (2 * L0 + L1) / 3.0f;
    float coef_r = height / L;
    corrV1 = v0 + (corrV1 - v0) * coef_r;
    corrV2 = corrV1 + (corrV2 - corrV1) * coef_r;
    
    
    curBlade.v1.xyz = corrV1;
    curBlade.v2.xyz = corrV2;

// ************************************ CULLING ***************************************

	// TODO: Cull blades that are too far away or not in the camera frustum and write them
	// to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads
    vec3 viewDir = normalize(v0 - camera.camPos);

    bool isCulled = false;
#if ORIENTATION_CULL
    // cull if width dir and viewDir align
    bool oriCull = abs(dot(t1, viewDir)) > 0.95f;
    isCulled = isCulled || oriCull;
#endif

#if FRUSTUM_CULL
    bool inside_m = false;
    float t = 0.1; // tolerance
    vec3 m = 1/4.f * v0 + 1/2.f * v1 + 1/4.f * v2;
    mat4 viewProj = camera.proj * camera.view;
    vec4 proj_m = viewProj * vec4(m, 1.0);
    float h = proj_m.w + t;
    inside_m = (proj_m.x > - h) && (proj_m.x < h);
    inside_m = inside_m && ((proj_m.y > - h) && (proj_m.y < h));
    inside_m = inside_m && ((proj_m.z > - h) && (proj_m.z < h));
    
    bool inside_v0 = false;
    vec4 proj_v0 = viewProj * vec4(v0, 1.0);
    h = proj_v0.w + t;
    inside_v0 = (proj_v0.x > - h) && (proj_v0.x < h);
    inside_v0 = inside_v0 && ((proj_v0.y > - h) && (proj_v0.y < h));
    inside_v0 = inside_v0 && ((proj_v0.z > - h) && (proj_v0.z < h));

    bool inside_v2 = false;
    vec4 proj_v2 = viewProj * vec4(v2, 1.0);
    h = proj_v2.w + t;
    inside_v2 = (proj_v2.x > - h) && (proj_v2.x < h);
    inside_v2 = inside_v2 && ((proj_v2.y > - h) && (proj_v2.y < h));
    inside_v2 = inside_v2 && ((proj_v2.z > - h) && (proj_v2.z < h));

    isCulled = isCulled || !(inside_m || inside_v0 || inside_v2);
#endif

#if DISTANCE_CULL
    float d_proj = length(v0 - camera.camPos - up * dot(v0 - camera.camPos, up));   // distance projected on the ground
    float d_max = 40.f;
    
    int cullIndx = int( N * (1 - min(1,(d_proj/d_max))) );
    bool disCull = ((bladeIdx % N) >= cullIndx);        // use > to keep 1 blade every N blades
    isCulled = isCulled || disCull;
#endif 


    inputBlades.blades[bladeIdx] = curBlade;        // try switching the order of this line and "if(isCulled) return;"
    if(isCulled) return;
    // atomicAdd returns the number before added.
    uint oldIdx = atomicAdd(numBlades.vertexCount, 1);
    outputBlades.blades[oldIdx] = curBlade;
}
